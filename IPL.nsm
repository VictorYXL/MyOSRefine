; Show Hello,world with NASM
; IPL.img
; Load program to 0x7c00
    ORG     0x7c00

; Format of floppy disk in fat12
    JMP     entry
    DB      0x90
    DB      "MyOS IPL"
    DW      512
    DB      1
    DW      1
    DB      2
    DW      224
    DW      2880
    DB      0xf0
    DW      9
    DW      18
    DW      2
    DD      0
    DD      2880
    DB      0, 0, 0x29
    DD      0xffffffff
    DB      "MyOS       "
    DB      "FAT12   "
    RESB    18

; Boot kernel
entry:
; Init register
    MOV     AX,0            
    MOV        SS,AX
    MOV        SP,0x7c00        
    MOV        DS,AX
    MOV        AX,0x0820

    MOV        ES,AX
    MOV        CH,0
    MOV        DH,0
    MOV        CL,2

read_sector:
    MOV        SI,0

try_read:
    MOV     AH,0x02
    MOV     AL,1
    MOV     BX,0
    MOV     DL,0x00
    INT     0x13
    JNC     next
    ADD     SI,1
    CMP     SI,5
    JAE        error
    MOV     AH,0x00
    MOV     DL,0x00
    INT     0x13
    JMP     try_read

next:
    MOV     AX,ES
    ADD     AX,0x0020
    MOV     ES,AX
    ADD     CL,1
    CMP     CL,18
    JBE     read_sector
        
    MOV     CL,1
    ADD     DH,1
    CMP     DH,1
    JBE     read_sector
        
    MOV     DH,0
    ADD     CH,1
    CMP     CH,9
    JBE     read_sector

    JMP     load_os

; Loop: print string with interrupt
print:
    MOV     AL, [SI]
    ADD     SI, 1
    CMP     AL, 0
    JE      end
    ; Show string with interrupt
    MOV     AH, 0x0e
    MOV     BX, 15
    INT     0x10
    JMP     print
; Wait
end:
    HLT
    JMP     end
; load os
load_os:
    MOV     [0x0ff0],CH
    JMP     0xc200            
; error
error:
    MOV     SI, error_msg
    JMP     print
; error msg
error_msg:
    DB      "load error"
    DB      0
; Boot end
    TIMES   510 - ($ - $$) DB 0
    DB      0x55, 0xaa

; Output outside booting section
    DB      0xf0, 0xff, 0xff, 0x00, 0x00, 0x00, 0x00, 0x00
    RESB    4600
    DB      0xf0, 0xff, 0xff, 0x00, 0x00, 0x00, 0x00, 0x00
    RESB    1469432
